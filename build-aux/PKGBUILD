pkgname=portprotonqt
pkgver=0.1.1
pkgrel=1
pkgdesc="A modern GUI for PortProton project."
arch=('any')
url="https://github.com/Boria138/PortProtonQt"
license=('MIT')
depends=('python-numpy' 'python-requests' 'python-babel' 'python-evdev' 'python-pyudev' 'python-orjson'
    'python-psutil' 'python-tqdm' 'python-vdf' 'pyside6' 'icoextract' 'python-pillow' 'perl-image-exiftool')
makedepends=('python-'{'build','installer','setuptools','wheel'})
source=("git+https://github.com/Boria138/PortProtonQt.git#tag=$pkgver")
sha256sums=('SKIP')

build() {
    cd "$srcdir/PortProtonQt"
	python -m build --wheel --no-isolation
}

package() {
    cd "$srcdir/PortProtonQt"
    python -m installer --destdir="$pkgdir" dist/*.whl
    cp -r build-aux/share "$pkgdir/usr/"
}
